buildscript {
	    repositories {	      
	        mavenCentral()	
	        mavenLocal()        
	    }
	
	    dependencies {
	        //classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
	        classpath 'org.akhikhl.gretty:gretty:+'
	        
	    }
	}
 
configure(allprojects) { project ->
	group = PROJ_GROUP
	version = PROJ_VERSION	
	apply plugin: "java"
	apply plugin: "maven-publish"
	apply plugin: "eclipse"
	apply plugin: "idea"
	
	eclipse {
		jdt {
			//if you want to alter the java versions (by default they are configured with gradle java plugin settings):
			sourceCompatibility = SOURCE_COMPILER_LEVEL
			targetCompatibility = TARGET_COMPILER_LEVEL
			javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"

		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	tasks.withType(Javadoc) {
		options.encoding = 'UTF-8'
	}

	jar {
		manifest {
			attributes (
					'Implementation': version,
					'Specification-Version': version,
					'Implementation-Vendor': 'bbossgroups',
					'Implementation-ProductID': project.name,
					'Compile-Timestamp': new Date().format('yyyy-MM-dd HH:mm:ss'),
					'Compile-User': DEVELOPER_NAME
			)
		}
	}

	dependencies {
		testCompile 'junit:junit:4.12'
	}

	repositories {
		mavenCentral()
		mavenLocal()
	}
	if(project.getProperty('skipTest'))
	{
		//test.onlyIf { !project.getProperty('skipTest') } 
		test.enabled = false  
	}
}

configure(subprojects.findAll {it.name.equals('bboss-websocket')}) {
	
    apply plugin: 'war'
    /**apply plugin: 'jetty'  
    jettyRun {
	  reload ="automatic"
	  scanIntervalSeconds = 1
	  httpPort=80
	  contextPath='/bboss-websocket'
	}*/
    apply plugin: 'org.akhikhl.gretty'
    
     gretty {
	    httpPort = 80
	   
	    contextPath = '/bboss-websocket'
	    jvmArgs = ['-Xmx1024M', '-XX:PermSize=128M', '-XX:MaxPermSize=256M']
	  //  servletContainer = 'jetty7'
	  
	  }
    /**evaluationDependsOn(':bboss-mvc')
	evaluationDependsOn(':bboss-util')
	evaluationDependsOn(':bboss-core')
	evaluationDependsOn(':bboss-soa')*/
}
configure(subprojects - [project(":bboss-util"),project(":bboss-taglib"),project(":bboss-websocket")]) { subproject ->
	task sourcesJar(type: Jar) {
		classifier = "sources"	
		from sourceSets.main.allJava
	}
	
	task javaDocJar(type: Jar, dependsOn: javadoc) {
		classifier = "javadoc"
		from javadoc.destinationDir
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact sourcesJar
				artifact javaDocJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}